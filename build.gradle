plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
    id 'maven-publish'
}

group 'WildInspect'
version = "2024.1"

project.ext {
    archiveFolder = file("archive/")
    targetFolder = file("target/")
    buildVersion = System.getenv("BUILD_NUMBER") == null || Boolean.parseBoolean(System.getenv("STABLE_BUILD")) ?
            version : version + "-b" + System.getenv("BUILD_NUMBER")
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'io.github.goooler.shadow'

    repositories {
        maven { url 'https://repo.bg-software.com/repository/nms/' }
        maven { url 'https://repo.bg-software.com/repository/api/' }
        maven { url 'https://repo.bg-software.com/repository/common/' }
        maven { url 'https://repo.bg-software.com/repository/dependencies/' }
    }

    dependencies {
        compileOnly "com.bgsoftware.common.reflection:ReflectionUtils:b3"
    }

    task checkDebug() {
        Set<File> filesWithDebug = fileTree('src/main/java').filter { file ->
            file.text.contains('Bukkit.broadcastMessage')
        }.getFiles()

        if (!filesWithDebug.isEmpty())
            throw new GradleException("Found debug messages: " + filesWithDebug)
    }

    build {
        dependsOn checkDebug
        dependsOn shadowJar
    }
}

subprojects {
    shadowJar {
        archiveFileName = "${project.name}.jar"
        destinationDirectory = rootProject.archiveFolder
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation fileTree(rootProject.archiveFolder.getAbsolutePath())

    implementation 'com.bgsoftware.common.reflection:ReflectionUtils:b3'
    implementation 'com.bgsoftware.common.config:CommentedConfiguration:b1'

    implementation 'org.bstats:bstats-bukkit:3.0.0'

    // Spigot jar
    compileOnly "org.spigotmc:v1_8_R3-Taco:latest"
    // CoreProtect
    compileOnly 'net.coreprotect:CoreProtect:19.0'
}

jar {
    from {
        for (File file : rootProject.archiveFolder.listFiles()) {
            zipTree(file)
        }
    }
}

processResources {
    outputs.upToDateWhen {false}
    eachFile { details ->
        if (details.name.contentEquals('plugin.yml')) {
            filter { String line ->
                line.replace('${project.version}', rootProject.buildVersion)
            }
        }
    }
}

shadowJar {
    dependsOn(jar)

    relocate 'org.bstats', 'com.bgsoftware.wildinspect.libs.org.bstats'
    relocate 'com.bgsoftware.common', 'com.bgsoftware.wildinspect.libs.com.bgsoftware.common'

    archiveFileName = rootProject.name + "-" + rootProject.buildVersion + ".jar"

    delete fileTree(rootProject.targetFolder.getAbsolutePath())

    destinationDirectory = rootProject.targetFolder
    from sourceSets.getByName("main").output
    configurations = [project.configurations.getByName("runtimeClasspath")]
}

clean {
    delete rootProject.archiveFolder
}

build {
    dependsOn shadowJar
    dependsOn clean
}

publish.shouldRunAfter shadowJar
shadowJar.shouldRunAfter build
build.shouldRunAfter subprojects.build

shadowJar.dependsOn subprojects.build
compileJava.dependsOn childProjects.values().shadowJar

compileJava.options.encoding = 'UTF-8'